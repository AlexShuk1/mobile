
#Область  ОбработчикиКомандФормы


&НаКлиенте
Процедура Войти(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) или Модифицированность Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Необходимо записать подключение";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеВводаШтрихкодаСотрудника",ЭтаФорма);
	ПараметрыФормы = Новый Структура();
	ОткрытьФорму("ОбщаяФорма.ФормаВводаШтрихкода",ПараметрыФормы,ЭтаФорма,,,,Оповещение,РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);	
	
КонецПроцедуры      


#КонецОбласти


#Область СлужебныеПроцедурыИФункции   


&НаКлиенте
Процедура ПослеВводаШтрихкодаСотрудника(Результат, ДополнительныеПараметры) Экспорт 

	Если ЗначениеЗаполнено(Результат) Тогда 
    	Соединение = SS_СлужебныйКлиентСервер.ПолучитьСоединениеСБазой(Объект); 
		Запрос = SS_СлужебныйКлиентСервер.ПолучитьHTTPЗапрос();
		Запрос.АдресРесурса = Объект.ИмяБазы+"/hs/tsd/authentication/"+Результат;   
		
		Попытка
		
			Ответ = Соединение.Получить(Запрос); 
			
			Если Ответ.КодСостояния = 200 Тогда
				ПолученныеДанные = SS_СлужебныйКлиентСервер.ПрочитатьТелоJSON(Ответ);
				Если ПолученныеДанные.status = "success" Тогда       
					SS_СлужебныйСервер.УстановитьТекущееПодключение(Объект.Ссылка, Результат);
					Оповестить("УспешноеПодключениеКИнформационнойБазе");  
					Закрыть();
				Иначе
					Сообщение = Новый СообщениеПользователю;
					Сообщение.Текст = "Ошибка входа "+ПолученныеДанные.message;
					Сообщение.Сообщить();	
				КонецЕсли;  
				
			Иначе
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Ошибка подключения. Код ответа "+Ответ.КодСостояния;
				Сообщение.Сообщить();
			КонецЕсли;  
			
		Исключение   
			Сообщение = Новый СообщениеПользователю;  
			ТекстОшибки = ОписаниеОшибки();
			Сообщение.Текст = "Ошибка приложения. Обратитесь в поддержку";
			Сообщение.Сообщить();
		КонецПопытки;
				
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти