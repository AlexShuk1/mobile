
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РабочееМесто = МенеджерОборудованияВызовСервера.ПолучитьРабочееМестоКлиента();
	
	//СписокОборудования = МенеджерОборудованияВызовСервера.ОборудованиеПоПараметрам(,,РабочееМесто); 
	//Для Каждого ЭкземплярОборудование Из СписокОборудования Цикл
	//	Элементы.Оборудование.СписокВыбора.Добавить(ЭкземплярОборудование.Ссылка);
	//КонецЦикла;
	
	ККТ_КодКонтрольнойМаркиСтрока = "010406205871178521.Bb""adi,ELNkc\u001d91FFD0\u001d92dGVzdCXjohbwmtUivYwTxu8f9F4KY4yt7oWMUdlZchw=";
	
	КассирФИО = "Сидоров И.П.";
	КассирДолжность = "АДМИНИСТРАТОР";
	КассирИНН = "780100325933";
	
	ПолучательИНН = "500100732259";
	Получатель = "Пупкин В.В.";
	
	ОрганизацияИНН = "7709860400";
	ОрганизацияКПП = "770901001";
	
	ТелефонАгента = "+78005550606";
	ТелефонПоставщика = "+78002500505";
	
	СистемаНалогообложения = Перечисления.ТипыСистемНалогообложенияККТ.ОСН;
	
	Сумма = 100;
	ТипРасчета = Перечисления.ТипыРасчетаДенежнымиСредствами.ПриходДенежныхСредств;

	ВремТовар = Товары.Добавить();
	СтрокаТаблицыТоваров = Новый Структура();
	ВремТовар.Наименование = "Мука пшеничная (Продукт первой необходимости)";   
	ВремТовар.Количество   = 0.992;  
	ВремТовар.Цена         = 15;
	ВремТовар.Сумма        = 13.84;  
	ВремТовар.НомерСекции  = 1;   
	ВремТовар.СтавкаНДС    = "НДС10%";   
	ВремТовар.ПризнакСпособаРасчета  = Перечисления.ПризнакиСпособаРасчета.ПередачаСПолнойОплатой;
	ВремТовар.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.Товар;
	
	ВремТовар = Товары.Добавить();
	СтрокаТаблицыТоваров = Новый Структура();
	ВремТовар.Наименование = "Томатный сок в бутылке 0.5 л";   
	ВремТовар.Количество   = 2;  
	ВремТовар.Цена         = 150;
	ВремТовар.Сумма        = 320;  
	ВремТовар.НомерСекции  = 2;   
	ВремТовар.СтавкаНДС    = "НДС20%";  
	ВремТовар.ПризнакСпособаРасчета  = Перечисления.ПризнакиСпособаРасчета.ПередачаСПолнойОплатой;
	ВремТовар.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.Товар;
	
	ВремТовар = Товары.Добавить();
	СтрокаТаблицыТоваров = Новый Структура();
	ВремТовар.Наименование = "Пакет";   
	ВремТовар.Количество   = 1;  
	ВремТовар.Цена         = 10;
	ВремТовар.Сумма        = 10;  
	ВремТовар.НомерСекции  = 3;   
	ВремТовар.СтавкаНДС    = "БезНДС%";  
	ВремТовар.ПризнакСпособаРасчета  = Перечисления.ПризнакиСпособаРасчета.ПередачаСПолнойОплатой;
	ВремТовар.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.Товар;
	
	ВремОплата = Оплата.Добавить();
	ВремОплата.ТипОплаты = Перечисления.ТипыОплатыККТ.Наличные;
	ВремОплата.Сумма = 150;
	ВремОплата = Оплата.Добавить();
	ВремОплата.ТипОплаты = Перечисления.ТипыОплатыККТ.Электронно;
	ВремОплата.Сумма = 200;
	ВремОплата = Оплата.Добавить();
	ВремОплата.ТипОплаты = Перечисления.ТипыОплатыККТ.Предоплата;
	ВремОплата.Сумма = 0;
	ВремОплата = Оплата.Добавить();
	ВремОплата.ТипОплаты = Перечисления.ТипыОплатыККТ.Постоплата;
	ВремОплата.Сумма = 0;
	ВремОплата = Оплата.Добавить();
	ВремОплата.ТипОплаты = Перечисления.ТипыОплатыККТ.ВстречноеПредоставление;
	ВремОплата.Сумма = 0;
	
	СтрокиТекста = "Мой дядя самых честных правил,
		|Когда не в шутку занемог,
		|Он уважать себя заставил,
		|И лучше выдумать не мог;
		|Его пример другим наука:
		|Но, боже мой, какая скука
		|С больным сидеть и день, и ночь,
		|Не отходя ни шагу прочь!    
		|[cut]
		|Какое низкое коварство
		|Полуживого забавлять,
		|Ему подушки поправлять,
		|Печально подносить лекарство,
		|Вздыхать и думать про себя:
		|Когда же черт возьмет тебя!";
		
	ТипШтрихкода = "EAN13";
	ШтрихКод     = "2900001462105";
	
	ЭТСумма = 100;  
	ЭТНомерМерчанта = 0;
	
	ОписаниеКоррекции  = "Ошибка применения ККТ";
	ДокументОснованиеФП = "4208773893";
	НомерПредписания = 100;
	ДатаКоррекции = ТекущаяДата();
	
	ККТ_Количество = 1;
	ККТ_ПланируемыйСтатусТовара = Перечисления.ПланируемыйСтатусМаркируемогоТовара.ШтучныйТоварРеализован;
	ККТ_МераКоличества = Перечисления.МераКоличестваПредметаРасчетаККТ.Штука;
	ККТ_ОжидатьПолучениеОтветаОИСМ = Истина;
	ККТ_БудетРеализована = Истина;
	ФФД12 = Истина;
	ТестовыеДанные = Истина;
	
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.Оборудование);
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.ОборудованиеУстройствоВвода);
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.ОборудованиеПлатежнаяСистема);
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.ОборудованиеПериферия);
	
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.ТипРасчета);
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.КассирФИО);
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.КассирИНН);
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.КассирДолжность);
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.СистемаНалогообложения);
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.Получатель);
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.ПолучательИНН);
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.ОрганизацияИНН);
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.ОрганизацияКПП);
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.ТелефонйАгента);
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.ТелефонПоставщика);
	
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.НомерЧека);
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.Сумма);
	
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.ТипШтрихкода);
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.Штрихкод);
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.СтрокиТекста);
	
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.ЭТСумма);
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.ЭТНомерЧека);
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.ЭТНомерКарты);
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.ЭТНомерСсылкиОперации);
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.ЭТКодАвторизации);
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.ЭТНомерМерчанта);
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.ЭТСлипЧек);
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.ДП_Текст);
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.ДП_ЗначениеQRКода);
	
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.НеприменениеККТ);
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.ОписаниеКоррекции);
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.ТипКоррекции);
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.ДополнительныйРеквизит);
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.ДатаКоррекции);
	МенеджерОборудованияВызовСервера.ПодготовитьЭлементУправления(Элементы.НомерПредписания);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьВерсиюДрайвераУстройстваПечати(Команда)
	
	ПолучитьВерсиюДрайвера(ОборудованиеУстройстваПечати);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьВерсиюДрайвераУстройстваВвода(Команда)
	
	ПолучитьВерсиюДрайвера(ОборудованиеУстройствоВвода);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьВерсиюДрайвераПлатежнаяСистема(Команда)
	
	ПолучитьВерсиюДрайвера(ОборудованиеПлатежнаяСистема);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьВерсиюДрайвера(ПодключаемоеОборудование)
	
	ОчиститьСообщения();
	ВыходныеПараметры = Неопределено;
	Если МенеджерОборудованияКлиент.ПолучитьОписаниеДрайвера(УникальныйИдентификатор, ПодключаемоеОборудование, Неопределено, ВыходныеПараметры) Тогда
		
		ТекстСообщения = "";
		
		Если НЕ ПустаяСтрока(ВыходныеПараметры.РевизияИнтерфейса) Тогда
			ТекстСообщения = "Ревизия драйвера:" + ВыходныеПараметры.РевизияИнтерфейса + Символы.ПС;
		КонецЕсли;
		
		Если НЕ ПустаяСтрока(ВыходныеПараметры.ВерсияДрайвера) Тогда
			ТекстСообщения = ТекстСообщения + "Версия драйвера установленная:" + ВыходныеПараметры.ВерсияДрайвера + Символы.ПС;
		КонецЕсли;
		
		Если НЕ ПустаяСтрока(ВыходныеПараметры.ВерсияДрайвераВМакете) Тогда
			ТекстСообщения = ТекстСообщения + "Версия драйвера в макете:" + ВыходныеПараметры.ВерсияДрайвераВМакете + Символы.ПС;
		КонецЕсли;
		
		Сообщить(ТекстСообщения);
		
	КонецЕсли;   
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПроверкуГотовностиУстройства(Команда)
	
	ВыходныеПараметры = Неопределено;
	ОчиститьСообщения();
	
	Если МенеджерОборудованияКлиент.ВыполнитьПроверкуГотовностиУстройства(ОборудованиеУстройстваПечати, ВыходныеПараметры) Тогда
		Сообщить("Устройство доступно.");
	Иначе
		Сообщить(ВыходныеПараметры.ТекстОшибки);
	КонецЕсли;   
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПроверкуГотовностиЭквайринга(Команда)
	
	ВыходныеПараметры = Неопределено;
	ОчиститьСообщения();
	
	Если МенеджерОборудованияКлиент.ВыполнитьПроверкуГотовностиУстройства(ОборудованиеПлатежнаяСистема, ВыходныеПараметры) Тогда
		Сообщить("Устройство доступно.");
	Иначе
		Сообщить(ВыходныеПараметры.ТекстОшибки);
	КонецЕсли;   
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСтавкуНДС(СтавкаНДС)
	
	Если СтавкаНДС = "НДС10%" Тогда
		Результат = 10
	ИначеЕсли СтавкаНДС = "НДС20%" Тогда
		Результат = 20
	ИначеЕсли СтавкаНДС = "НДС18%" Тогда
		Результат = 18
	ИначеЕсли СтавкаНДС = "НДС0%" Тогда
		Результат = 0
	ИначеЕсли СтавкаНДС = "НДС10/110" Тогда
		Результат = 110
	ИначеЕсли СтавкаНДС = "НДС18/118" Тогда
		Результат = 118
	ИначеЕсли СтавкаНДС = "НДС20/120" Тогда
		Результат = 120
	Иначе
		Результат = Неопределено; // Без НДС
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьШаблонЧекаДоп(ОбщиеПараметры)
	
	// Общие параметры чека
	ОбщиеПараметры = МенеджерОборудованияКлиентСервер.ПараметрыОперацииЧекаКоррекции();
	
	// Параметры установлены по умолчанию.
	ОбщиеПараметры.ТипРасчета = ТипРасчета;
	ОбщиеПараметры.Электронно = Ложь;
	
	ОбщиеПараметры.ПокупательEmail = Неопределено;
	ОбщиеПараметры.ПокупательНомер = Неопределено;
	// Параметры необходимые для чека ЕНВД на принтере чеков
	
	ОбщиеПараметры.Кассир     = КассирФИО;
	ОбщиеПараметры.КассирИНН  = КассирИНН;
	
	ОбщиеПараметры.СистемаНалогообложения = СистемаНалогообложения;
	ОбщиеПараметры.ОрганизацияНазвание = "ООО ""1C-СОФТ""";
	ОбщиеПараметры.ОрганизацияИНН = ОрганизацияИНН;
	ОбщиеПараметры.ОрганизацияКПП = ОрганизацияКПП;
	
	ОбщиеПараметры.НомерКассы     = "00001";
	ОбщиеПараметры.НомерЧека      = "1";
	ОбщиеПараметры.НомерСмены     = "1";
	ОбщиеПараметры.АдресРасчетов  = "г.Москва, Дмитровское ш. д.9";
	ОбщиеПараметры.МестоРасчетов  = "Торговый островок на 1 этаже";
	
	ОбщиеПараметры.ОперационныйРеквизит.ДатаВремяОперации  = ТекущаяДата();
	ОбщиеПараметры.ОперационныйРеквизит.ИдентификаторОперации = 0;
	ОбщиеПараметры.ОперационныйРеквизит.ДанныеОперации = "DATA";
	
	ОбщиеПараметры.ОтраслевойРеквизит.ИдентификаторФОИВ  = "001";
	ОбщиеПараметры.ОтраслевойРеквизит.ДатаДокументаОснования = ТекущаяДата();
	ОбщиеПараметры.ОтраслевойРеквизит.НомерДокументаОснования = "2343243";
	ОбщиеПараметры.ОтраслевойРеквизит.ЗначениеРеквизита = "Ид1=Знач1&Ид2=Знач2&Ид3=Знач3";
	
	ОбщиеПараметры.СведенияОПокупателе.Покупатель =  Получатель;
	ОбщиеПараметры.СведенияОПокупателе.ПокупательИНН = ПолучательИНН;
	ОбщиеПараметры.СведенияОПокупателе.ПокупательАдрес = ОбщиеПараметры.АдресРасчетов;
	ОбщиеПараметры.ЕстьПерсональныеДанные = Истина; 
	
	Если ДанныеАгента Тогда
		ОбщиеПараметры.ПризнакАгента = Перечисления.ПризнакиАгента.Агент;
		ОбщиеПараметры.ДанныеАгента.ПлатежныйАгент.Операция = "Оплата";
		ОбщиеПараметры.ДанныеАгента.ПлатежныйАгент.Телефон = ТелефонАгента;
		ОбщиеПараметры.ДанныеАгента.ОператорПеревода.Телефон = ТелефонПоставщика;
		ОбщиеПараметры.ДанныеАгента.ОператорПеревода.Наименование = "КИВИ Банк (АО)";
		ОбщиеПараметры.ДанныеАгента.ОператорПеревода.Адрес = "117648, г.Москва, мкр.Чертаново Северное, д.1А, корп.1";
		ОбщиеПараметры.ДанныеАгента.ОператорПеревода.ИНН = ОрганизацияИНН;
		ОбщиеПараметры.ДанныеПоставщика.Телефон = ТелефонПоставщика;
		ОбщиеПараметры.ДанныеПоставщика.Наименование = Получатель;
		ОбщиеПараметры.ДанныеПоставщика.ИНН = ПолучательИНН;
	КонецЕсли;

	// Фискальная строка 1
	СтрокаПозицииЧека = МенеджерОборудованияКлиентСервер.ПараметрыФискальнойСтрокиЧека(); 
	СтрокаПозицииЧека.Наименование = "Товарная позиция";
	СтрокаПозицииЧека.Количество   = 1;
	СтрокаПозицииЧека.Цена         = 100;
	СтрокаПозицииЧека.Сумма        = 100;
	СтрокаПозицииЧека.НомерСекции  = 1;
	СтрокаПозицииЧека.СтавкаНДС    = Неопределено;
	СтрокаПозицииЧека.КодЕдиницыИзмерения = "796";
	СтрокаПозицииЧека.Штрихкод = "RU-430301-AAA0020659";
	СтрокаПозицииЧека.ПризнакСпособаРасчета  = Перечисления.ПризнакиСпособаРасчета.ПередачаСПолнойОплатой;
	СтрокаПозицииЧека.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.Товар;
	СтрокаПозицииЧека.КодЕдиницыИзмерения = "796";
	ОбщиеПараметры.ПозицииЧека.Добавить(СтрокаПозицииЧека);
	
	// Нефискальная строка
	ТекстоваяСтрокаЧека = МенеджерОборудованияКлиентСервер.ПараметрыТекстовойСтрокиЧека();
	ТекстоваяСтрокаЧека = МенеджерОборудованияКлиентСервер.ПараметрыТекстовойСтрокиЧека("Для подключение оборудования к автоматизированным системам на платформе «1С:Предприятие» используется ""1С:Библиотека подключаемого оборудования"". Библиотека предоставляет набор высокоуровневых механизмов для унифицированной работы с подключаемым оборудованием. Применение данной библиотеки позволяет ускорить реализацию поддержки широкого спектра моделей подключаемого оборудования в разрабатываемых конфигурациях, а также добавить готовые функциональные блоки в уже работающие прикладные решения.");
	ТекстоваяСтрокаЧека.ПереносСтроки = Истина;
	ОбщиеПараметры.ПозицииЧека.Добавить(ТекстоваяСтрокаЧека);
	
	// Фискальная строка 2
	СтрокаПозицииЧека = МенеджерОборудованияКлиентСервер.ПараметрыФискальнойСтрокиЧека(); 
	СтрокаПозицииЧека.Наименование = "Оплата за телефон";
	СтрокаПозицииЧека.Количество   = 1;
	СтрокаПозицииЧека.Цена         = 100;
	СтрокаПозицииЧека.Сумма        = 100;
	СтрокаПозицииЧека.НомерСекции  = 1;
	СтрокаПозицииЧека.СтавкаНДС    = 10;
	СтрокаПозицииЧека.СуммаНДС = Окр(СтрокаПозицииЧека.Сумма / 110 * 10, 2);
	СтрокаПозицииЧека.КодЕдиницыИзмерения = "796";
	
	Если ДанныеАгента Тогда
		СтрокаПозицииЧека.ДанныеАгента.ПлатежныйАгент.Операция = "Оплата";
		СтрокаПозицииЧека.ДанныеАгента.ПлатежныйАгент.Телефон = ТелефонАгента;
		СтрокаПозицииЧека.ДанныеПоставщика.Телефон = ТелефонПоставщика;
		СтрокаПозицииЧека.ДанныеПоставщика.Наименование = Получатель;
		СтрокаПозицииЧека.ДанныеПоставщика.ИНН = ПолучательИНН;
	КонецЕсли;
	
	СтрокаПозицииЧека.ПризнакСпособаРасчета  = Перечисления.ПризнакиСпособаРасчета.ПредоплатаПолная;
	СтрокаПозицииЧека.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.Услуга;
	ОбщиеПараметры.ПозицииЧека.Добавить(СтрокаПозицииЧека);
	
	// Фискальная строка 3
	СтрокаПозицииЧека = МенеджерОборудованияКлиентСервер.ПараметрыФискальнойСтрокиЧека(); 
	СтрокаПозицииЧека.Наименование = "Другая товарная позиция c очень длинным наименование для проверки";
	СтрокаПозицииЧека.Количество   = 2;
	СтрокаПозицииЧека.Цена         = 100;
	СтрокаПозицииЧека.Сумма        = 150;
	СтрокаПозицииЧека.СуммаСкидок  = 50;
	СтрокаПозицииЧека.НомерСекции  = 1;
	СтрокаПозицииЧека.КодЕдиницыИзмерения = "796";
	СтрокаПозицииЧека.СтавкаНДС  = 20;
	СтрокаПозицииЧека.СуммаНДС = Окр(СтрокаПозицииЧека.Сумма / 120 * 20, 2);
	СтрокаПозицииЧека.ПризнакСпособаРасчета  = Перечисления.ПризнакиСпособаРасчета.Аванс;
	СтрокаПозицииЧека.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.Товар;
	ОбщиеПараметры.ПозицииЧека.Добавить(СтрокаПозицииЧека);
	
	// Фискальная строка 4
	СтрокаПозицииЧека = МенеджерОборудованияКлиентСервер.ПараметрыФискальнойСтрокиЧека(); 
	СтрокаПозицииЧека.Наименование = "Тобачная продукция";
	СтрокаПозицииЧека.Количество   = 1;
	СтрокаПозицииЧека.Цена         = 30;
	СтрокаПозицииЧека.Сумма        = 30;
	СтрокаПозицииЧека.СуммаСкидок  = 0;
	СтрокаПозицииЧека.НомерСекции  = 3;
	СтрокаПозицииЧека.СтавкаНДС    = Неопределено;
	СтрокаПозицииЧека.ПризнакСпособаРасчета  = Перечисления.ПризнакиСпособаРасчета.ПередачаСПолнойОплатой;
	СтрокаПозицииЧека.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.Товар;
	СтрокаПозицииЧека.КодЕдиницыИзмерения = "796";
	
	Если Не ПустаяСтрока(ККТ_КодКонтрольнойМарки) Тогда
		КодКонтрольнойМарки = ККТ_КодКонтрольнойМарки;  
		Если ФФД12 Тогда
			СтрокаПозицииЧека.КонтрольнаяМарка = КодКонтрольнойМарки;
		Иначе
			СтрокаПозицииЧека.ШтрихкодBase64  = КодКонтрольнойМарки;
		КонецЕсли;
		
		ШтучныйТовар = ККТ_ПланируемыйСтатусТовара = ПредопределенноеЗначение("Перечисление.ПланируемыйСтатусМаркируемогоТовара.ШтучныйТоварРеализован")
			Или ККТ_ПланируемыйСтатусТовара = ПредопределенноеЗначение("Перечисление.ПланируемыйСтатусМаркируемогоТовара.ШтучныйТоварВозвращен");
		Если Не ШтучныйТовар Тогда
			СтрокаПозицииЧека.ДробноеКоличество.Числитель = 1; 
			СтрокаПозицииЧека.ДробноеКоличество.Знаменатель = 6;
		КонецЕсли;
	
	КонецЕсли;
	
	ОбщиеПараметры.ПозицииЧека.Добавить(СтрокаПозицииЧека);
	
	// Фискальная строка 5
	СтрокаПозицииЧека = МенеджерОборудованияКлиентСервер.ПараметрыФискальнойСтрокиЧека(); 
	СтрокаПозицииЧека.Наименование = "Яблоки на развес";
	СтрокаПозицииЧека.Количество   = 0.61;
	СтрокаПозицииЧека.Цена         = 172.5;
	СтрокаПозицииЧека.Сумма        = 105.23;
	СтрокаПозицииЧека.НомерСекции  = 3;
	СтрокаПозицииЧека.СтавкаНДС = 20;
	СтрокаПозицииЧека.СуммаНДС = Окр(СтрокаПозицииЧека.Сумма / 120 * 20, 2);
	СтрокаПозицииЧека.ПризнакСпособаРасчета  = Перечисления.ПризнакиСпособаРасчета.ПередачаСПолнойОплатой;
	СтрокаПозицииЧека.ПризнакПредметаРасчета = Перечисления.ПризнакиПредметаРасчета.Товар;
	СтрокаПозицииЧека.КодЕдиницыИзмерения = "166";
	ОбщиеПараметры.ПозицииЧека.Добавить(СтрокаПозицииЧека);
	
	СтрокаПозицииЧека = МенеджерОборудованияКлиентСервер.ПараметрыТекстовойСтрокиЧека("Накопление по карте: 234543443");
	ОбщиеПараметры.ПозицииЧека.Добавить(СтрокаПозицииЧека);
	
	СтрокаПозицииЧека = МенеджерОборудованияКлиентСервер.ПараметрыТекстовойСтрокиЧека("Другая товарная позиция c очень длинным наименование для проверки");
	СтрокаПозицииЧека.ПереносСтроки = Истина;
	ОбщиеПараметры.ПозицииЧека.Добавить(СтрокаПозицииЧека);
	
	СтрокаПозицииЧека = МенеджерОборудованияКлиентСервер.ПараметрыТекстовойСтрокиЧека("Другая f");
	СтрокаПозицииЧека.Выравнивание  = "Центр";
	ОбщиеПараметры.ПозицииЧека.Добавить(СтрокаПозицииЧека);
	
	СтрокаПозицииЧека = МенеджерОборудованияКлиентСервер.ПараметрыШтрихкодВСтрокеЧека("EAN13", "2900001462105");
	ОбщиеПараметры.ПозицииЧека.Добавить(СтрокаПозицииЧека);
	
	СтрокаОплаты = МенеджерОборудованияКлиентСервер.ПараметрыСтрокиОплаты();
	СтрокаОплаты.Сумма = 1000; 
	СтрокаОплаты.ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплатыККТ.Наличные");
	ОбщиеПараметры.ТаблицаОплат.Добавить(СтрокаОплаты);
		
	СтрокаОплаты = МенеджерОборудованияКлиентСервер.ПараметрыСтрокиОплаты();
	СтрокаОплаты.Сумма = 200; 
	СтрокаОплаты.ТипОплаты = ПредопределенноеЗначение("Перечисление.ТипыОплатыККТ.Электронно");
	ОбщиеПараметры.ТаблицаОплат.Добавить(СтрокаОплаты);
	
	ОбщиеПараметры.ДанныеКоррекции.ТипКоррекции      = 0;
	ОбщиеПараметры.ДанныеКоррекции.ОписаниеКоррекции = "Чек ККМ";
	ОбщиеПараметры.ДанныеКоррекции.ДатаКоррекции     = ТекущаяДата();
	ОбщиеПараметры.ДанныеКоррекции.НомерПредписания  = "101";
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьШаблонЧека(ОбщиеПараметры, ШтрихкодВЧеке = Ложь)
	
	НесколькоВидовОплат = Истина;
	
	// Параметры установлены по умолчанию.   
	ОбщиеПараметры.ТипРасчета = ТипРасчета;
	ОбщиеПараметры.СистемаНалогообложения = СистемаНалогообложения;
	ОбщиеПараметры.Электронно = Ложь;
	
	ОбщиеПараметры.ПокупательEmail = Неопределено;
	ОбщиеПараметры.ПокупательНомер = Неопределено;
	// Параметры необходимые для чека ЕНВД на принтере чеков
	ОбщиеПараметры.КассирФИО       = КассирФИО;
	ОбщиеПараметры.КассирИНН       = КассирИНН;
	ОбщиеПараметры.КассирДолжность = КассирДолжность;
	// ОбщиеПараметры.КодСистемыНалогообложения = КодСистемыНалогообложения;
	ОбщиеПараметры.ОрганизацияНазвание = "ООО ""1C-СОФТ""";
	ОбщиеПараметры.ОрганизацияИНН = ОрганизацияИНН;
	ОбщиеПараметры.ОрганизацияКПП = ОрганизацияКПП;
	ОбщиеПараметры.НомерКассы     = "00001";
	ОбщиеПараметры.НомерЧека      = "1";
	ОбщиеПараметры.НомерСмены     = "1";
	ОбщиеПараметры.АдресРасчетов  = "г.Москва, Дмитровкое ш. д.9";
	ОбщиеПараметры.МестоРасчетов  = "Учебный центр 1С";
	ОбщиеПараметры.Получатель     = Получатель;
	ОбщиеПараметры.ПолучательИНН  = ПолучательИНН;
	
	НомерСтрокиТовара = 1;
	Для Каждого ВремТовар Из Товары Цикл
		СтрокаПозицииЧека = МенеджерОборудованияКлиентСервер.ПараметрыФискальнойСтрокиЧека(); 
		СтрокаПозицииЧека.Наименование = ВремТовар.Наименование;   
		СтрокаПозицииЧека.Количество   = ВремТовар.Количество;   
		СтрокаПозицииЧека.Цена         = ВремТовар.Цена;   
		СтрокаПозицииЧека.Сумма        = ВремТовар.Сумма;   
		СтрокаПозицииЧека.НомерСекции  = ВремТовар.НомерСекции; 
		СтрокаПозицииЧека.ПризнакСпособаРасчета  = ВремТовар.ПризнакСпособаРасчета;
		СтрокаПозицииЧека.ПризнакПредметаРасчета = ВремТовар.ПризнакПредметаРасчета;
		СтавкаНДС = ПолучитьСтавкуНДС(ВремТовар.СтавкаНДС);   
		СтрокаПозицииЧека.СтавкаНДС = СтавкаНДС; 
		Если Не ПустаяСтрока(СтавкаНДС) Тогда
			СтрокаПозицииЧека.СуммаНДС = Окр(СтрокаПозицииЧека.Сумма / (100 + СтавкаНДС) * СтавкаНДС, 2);
		Иначе
			СтрокаПозицииЧека.СуммаНДС = 0;
		КонецЕсли;
		СтрокаПозицииЧека.Штрихкод = "RU-430301-AAA0020659";
		СтрокаПозицииЧека.НомерСтрокиТовара = НомерСтрокиТовара; // Дополнительный параметр для обработки строк чека для форматно-логического контроля.
		НомерСтрокиТовара = НомерСтрокиТовара + 1;
		ОбщиеПараметры.ПозицииЧека.Добавить(СтрокаПозицииЧека);
	КонецЦикла;    
	
	СтрокаПозицииЧека = МенеджерОборудованияКлиентСервер.ПараметрыТекстовойСтрокиЧека("Предоставляемый библиотекой функционал является универсальным и не зависит от конкретных моделей оборудования различных производителей. После встраивания библиотеки в конфигурацию, данные функции доступны пользователям через предоставляемый библиотекой пользовательский интерфейс.");
	СтрокаПозицииЧека.ПереносСтроки = Истина;
	ОбщиеПараметры.ПозицииЧека.Добавить(СтрокаПозицииЧека);
	
	Если ШтрихкодВЧеке Тогда
		СтрокаПозицииЧека = МенеджерОборудованияКлиентСервер.ПараметрыШтрихкодВСтрокеЧека("EAN13", "2900001462105");
		ОбщиеПараметры.ПозицииЧека.Добавить(СтрокаПозицииЧека);
	КонецЕсли;
	
	// Таблица оплат
	ВремИндекс = 0;
	Для Каждого ВремОплата Из Оплата Цикл
		Если ВремОплата.Сумма > 0 Тогда
			СтрокаОплаты = Новый Структура();
			СтрокаОплаты.Вставить("ТипОплаты", ВремОплата.ТипОплаты); 
			СтрокаОплаты.Вставить("Сумма"    , ВремОплата.Сумма);
			ОбщиеПараметры.ТаблицаОплат.Добавить(СтрокаОплаты);
		КонецЕсли;
		ВремИндекс = ВремИндекс + 1;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредзаполнитьПараметрыЧека(ОбщиеПараметры);
	
	ОбщиеПараметры.Вставить("ОрганизацияНазвание" , "ООО ""Тестовая организация""");
	ОбщиеПараметры.Вставить("ОрганизацияИНН"      , ОрганизацияИНН);
	ОбщиеПараметры.Вставить("ОрганизацияКПП"      , ОрганизацияКПП);
	ОбщиеПараметры.Вставить("НомерКассы"          , "2334534");
	ОбщиеПараметры.Вставить("НомерЧека"           , "30");
	ОбщиеПараметры.Вставить("НомерСмены"          , "12");
	ОбщиеПараметры.Вставить("ТекстШапки"          , "ДОБРО ПОЖАЛОВАТЬ!");
	ОбщиеПараметры.Вставить("ТекстПодвала"        , "СПАСИБО ЗА ПОКУПКУ!");
	ОбщиеПараметры.Вставить("КассирФИО"           , КассирФИО);
	ОбщиеПараметры.Вставить("КассирДолжность"     , КассирДолжность);
	
КонецПроцедуры

&НаКлиенте
Процедура ККТ_ПечатьЭтикеток(Команда)
	
	ОчиститьСообщения();
	
	ЭтикеткиДляПечати = Новый Массив();
	
	Для Каждого ВремТовар Из Товары Цикл
		Этикетка = Новый Структура();
		НадписьЦена = "ЦЕНА: %% Руб.";
		НадписьЦена =СтрЗаменить(НадписьЦена, "%%", Формат(ВремТовар.Цена, "ЧЦ=10; ЧДЦ=2; ЧРД=.; ЧН=0.00; ЧГ=0"));
		Этикетка.Вставить("ТипШтрихкода", ТипШтрихкода);
		Этикетка.Вставить("Штрихкод"    , Штрихкод);
		Этикетка.Вставить("ШапкаЭтикетки" , "ООО ""Тестовая организация""" + Символы.ПС + ВремТовар.Наименование);
		Этикетка.Вставить("ПодвалЭтикетки", НадписьЦена);
		ЭтикеткиДляПечати.Добавить(Этикетка);
	КонецЦикла;
	
	ВходныеПараметры = Новый Структура("Этикетки", ЭтикеткиДляПечати);
	ВыходныеПараметры = Неопределено;
	Если НЕ МенеджерОборудованияКлиент.ВыполнитьПечатьЭтикеток(УникальныйИдентификатор, ОборудованиеУстройстваПечати, ВходныеПараметры, ВыходныеПараметры) Тогда
		Сообщить(ВыходныеПараметры.ТекстОшибки);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОборудованиеПриИзменении(Элемент)
	
	МенеджерОборудованияКлиент.ОтключитьВсеОборудование();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	МенеджерОборудованияКлиент.ОтключитьВсеОборудование();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ККТ_ПланируемыйСтатусТовараПриИзменении(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьШтрихкод(ДанныеВвода)
	
	ВремТипШтрихкода = МенеджерОборудованияКлиентСервер.ОпределитьТипШтрихкода(ДанныеВвода);
	
	ШтрихкодМаркировки = ДанныеВвода;
	
	Если ВремТипШтрихкода <> "EAN13" И ВремТипШтрихкода <> "EAN8" Тогда	
		Результат = МенеджерОборудованияКлиентСервер.РасшифроватьQRКодЧекаККТ(ДанныеВвода);
		Если Результат.Расшифрован Тогда
			Сообщить("Чек ККТ");
			ВывестиСтруктуруНаЭкран(Результат);
		Иначе
			Результат = МенеджерОборудованияМаркировкаКлиентСервер.РазобратьКодМаркировки(ДанныеВвода);
			Если Результат.Разобран Тогда
				Сообщить("Маркировки продукции");
				МаркировкаGTIN          = Результат.ГлобальныйИдентификаторТорговойЕдиницы;
				МаркировкаСерийныйНомер = Результат.СерийныйНомер;
				МаркировкаКодПроверки   = Результат.КодПроверки;
				Сообщить("GTIN:" + МаркировкаGTIN);
				Сообщить("Серийный номер:" + МаркировкаСерийныйНомер);
				Сообщить("Код проверки:" + МаркировкаКодПроверки);
			Иначе
				Сообщить("Штрихкод: " + ДанныеВвода);
			КонецЕсли;
		КонецЕсли;
	Иначе
		Сообщить("Штрихкод (" + ВремТипШтрихкода + "): " + ДанныеВвода);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьДанные(Команда)
	
	ВыходныеПараметры = Неопределено;
	Если МенеджерОборудованияКлиент.ВыполнитьВводДанных(УникальныйИдентификатор, ОборудованиеУстройствоВвода, ВыходныеПараметры) Тогда
		Если ВыходныеПараметры.Свойство("ДанныеВвода") Тогда
			 ОбработатьШтрихкод(ВыходныеПараметры.ДанныеВвода)
		КонецЕсли;
	Иначе
		Сообщить(ВыходныеПараметры.ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭС_Оплатить(Команда)
	
	ОчиститьСообщения();
	ЭТСлипЧек = "";
	
	ВыходныеПараметры = Неопределено; 
	
	ВходныеПараметры = МенеджерОборудованияКлиентСервер.ПараметрыВыполненияЭквайринговойОперации();
	ВходныеПараметры.Сумма     = ЭТСумма;
	ВходныеПараметры.НомерЧека = ЭТНомерЧека;
	ВходныеПараметры.НомерКарты = ЭТНомерКарты;
	ВходныеПараметры.НомерСсылкиОперации = ЭТНомерСсылкиОперации;
	ВходныеПараметры.КодАвторизации = ЭТКодАвторизации;    
	ВходныеПараметры.НомерМерчанта  = ЭТНомерМерчанта;
	
	Если Не МенеджерОборудованияКлиент.ВыполнитьОплатуПоПлатежнойСистеме(УникальныйИдентификатор, ОборудованиеПлатежнаяСистема, ВходныеПараметры, ВыходныеПараметры) Тогда
		Сообщить("ОТВЕТ:" + ВыходныеПараметры.ТекстОшибки);
	Иначе
		ЭТНомерКарты          = ВыходныеПараметры.НомерКарты;
		ЭТНомерСсылкиОперации = ВыходныеПараметры.НомерСсылкиОперации;
		ЭТКодАвторизации      = ВыходныеПараметры.КодАвторизации;
		ЭТСлипЧек             = ВыходныеПараметры.СлипЧек;
		Элементы.ОперацииЭС.ТекущаяСтраница = Элементы.СлипЧек;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭС_Отменить(Команда)
	
	ОчиститьСообщения();
	ЭТСлипЧек = "";
	
	ВыходныеПараметры = Неопределено;       
	
	ВходныеПараметры = МенеджерОборудованияКлиентСервер.ПараметрыВыполненияЭквайринговойОперации();
	ВходныеПараметры.Сумма     = ЭТСумма;
	ВходныеПараметры.НомерЧека = ЭТНомерЧека;
	ВходныеПараметры.НомерКарты = ЭТНомерКарты;
	ВходныеПараметры.НомерСсылкиОперации = ЭТНомерСсылкиОперации;
	ВходныеПараметры.КодАвторизации = ЭТКодАвторизации;    
	ВходныеПараметры.НомерМерчанта  = ЭТНомерМерчанта;
	
	Если Не МенеджерОборудованияКлиент.ВыполнитьОтменуПлатежаПоПлатежнойСистеме(УникальныйИдентификатор, ОборудованиеПлатежнаяСистема, ВходныеПараметры, ВыходныеПараметры) Тогда
		Сообщить(ВыходныеПараметры.ТекстОшибки);
	Иначе
		ЭТНомерКарты          = ВыходныеПараметры.НомерКарты;
		ЭТНомерСсылкиОперации = ВыходныеПараметры.НомерСсылкиОперации;
		ЭТКодАвторизации      = ВыходныеПараметры.КодАвторизации;
		ЭТСлипЧек             = ВыходныеПараметры.СлипЧек;
		Элементы.ОперацииЭС.ТекущаяСтраница = Элементы.СлипЧек;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭС_Вернуть(Команда)
	
	ОчиститьСообщения();
	ЭТСлипЧек = "";
	
	ВыходныеПараметры = Неопределено;
	
	ВходныеПараметры = МенеджерОборудованияКлиентСервер.ПараметрыВыполненияЭквайринговойОперации();
	ВходныеПараметры.Сумма     = ЭТСумма;
	ВходныеПараметры.НомерЧека = ЭТНомерЧека;
	ВходныеПараметры.НомерКарты = ЭТНомерКарты;
	ВходныеПараметры.НомерСсылкиОперации = ЭТНомерСсылкиОперации;
	ВходныеПараметры.КодАвторизации = ЭТКодАвторизации;    
	ВходныеПараметры.НомерМерчанта  = ЭТНомерМерчанта;
	
	Если Не МенеджерОборудованияКлиент.ВыполнитьВозвратПлатежаПоПлатежнойСистеме(УникальныйИдентификатор, ОборудованиеПлатежнаяСистема, ВходныеПараметры, ВыходныеПараметры) Тогда
		Сообщить(ВыходныеПараметры.ТекстОшибки);
	Иначе
		ЭТНомерКарты          = ВыходныеПараметры.НомерКарты;
		ЭТНомерСсылкиОперации = ВыходныеПараметры.НомерСсылкиОперации;
		ЭТКодАвторизации      = ВыходныеПараметры.КодАвторизации;
		ЭТСлипЧек             = ВыходныеПараметры.СлипЧек;
		Элементы.ОперацииЭС.ТекущаяСтраница = Элементы.СлипЧек;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭС_СверкаИтогов(Команда)
	
	ОчиститьСообщения();
	ЭТСлипЧек = "";
	
	ВыходныеПараметры = Неопределено;
	Если Не МенеджерОборудованияКлиент.ВыполнитьСверкуИтоговПоПлатежнойСистеме(УникальныйИдентификатор, ОборудованиеПлатежнаяСистема, ВыходныеПараметры) Тогда
		Сообщить(ВыходныеПараметры.ТекстОшибки);
	Иначе
		ЭТСлипЧек             = ВыходныеПараметры.СлипЧек;
		Элементы.ОперацииЭС.ТекущаяСтраница = Элементы.СлипЧек;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Штрихкод" Или ИмяСобытия = "Barcode" Тогда
		Штрихкод = Параметр;
		ОбработатьШтрихкод(Параметр);
	ИначеЕсли ИмяСобытия = "RFID" Тогда
		Если Параметр <> Неопределено Тогда
			Для Каждого Элемент Из Параметр Цикл
				МеткаRFID = МеткиRFID.Добавить();
				ЗаполнитьЗначенияСвойств(МеткаRFID, Элемент);
			КонецЦикла;
		КонецЕсли;
	Иначе
		Сообщить("Данные:" + Параметр);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ККТ_ШиринаСтроки(Команда)
	
	ОчиститьСообщения();
	ВыходныеПараметры = Неопределено;
	Если МенеджерОборудованияКлиент.ПолучитьШиринуСтрокиПечатающегоУстройства(УникальныйИдентификатор, ОборудованиеУстройстваПечати, Неопределено, ВыходныеПараметры) Тогда
		Сообщить("ШиринаСтроки:" + ВыходныеПараметры.ШиринаСтроки);
	Иначе
		Сообщить(ВыходныеПараметры.ТекстОшибки);
	КонецЕсли;   
	
КонецПроцедуры

&НаКлиенте
Процедура ККТ_ДенежныйЯщик(Команда)
	
	ОчиститьСообщения();
	ВыходныеПараметры = Неопределено;
	Если НЕ МенеджерОборудованияКлиент.ВыполнитьОткрытиеДенежногоЯщика(УникальныйИдентификатор, ОборудованиеУстройстваПечати, ВыходныеПараметры) Тогда
		Сообщить(ВыходныеПараметры.ТекстОшибки);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ККТ_ОткрытьСмену(Команда)
	
	ОчиститьСообщения();
	
	ВходныеПараметры = МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперации();
	ПредзаполнитьПараметрыЧека(ВходныеПараметры);
	ВыходныеПараметры = Неопределено;
	
	Если НЕ МенеджерОборудованияКлиент.ВыполнитьОткрытиеСменыНаФискальномУстройстве(УникальныйИдентификатор, ОборудованиеУстройстваПечати, ВходныеПараметры, ВыходныеПараметры) Тогда
		Сообщить(ВыходныеПараметры.ТекстОшибки);
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ККТ_ЗакрытьСмену(Команда)
	
	ОчиститьСообщения();
	
	ВходныеПараметры = МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперации();
	ПредзаполнитьПараметрыЧека(ВходныеПараметры);
	ВыходныеПараметры = Неопределено;
	
	Если НЕ МенеджерОборудованияКлиент.ВыполнитьЗакрытиеСменыНаФискальномУстройстве(УникальныйИдентификатор, ОборудованиеУстройстваПечати, ВходныеПараметры, ВыходныеПараметры) Тогда
		Сообщить(ВыходныеПараметры.ТекстОшибки);
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ККТ_ФискализацияЧека(Команда)
	
	ОчиститьСообщения();
	
	ВыходныеПараметры = Неопределено;
	
	ОбщиеПараметры = МенеджерОборудованияКлиентСервер.ПараметрыОперацииФискализацииЧека();
	
	Если ТестовыеДанные Тогда
		ЗаполнитьШаблонЧекаДоп(ОбщиеПараметры);
	Иначе
		ЗаполнитьШаблонЧека(ОбщиеПараметры, Истина);
	КонецЕсли;
	
	// Параметры для товарного чека и слип-чека ЕГАИС.
	ОбщиеПараметры.Вставить("ПодписьЕГАИС", ПодписьЕГАИС);
	ПредзаполнитьПараметрыЧека(ОбщиеПараметры);
	
	ФорматноЛогическийКонтрольКлиентСервер.ПровестиФорматноЛогическийКонтроль(ОбщиеПараметры, ОборудованиеУстройстваПечати);
	
	Отказ = Ложь;
	ОписаниеОшибки = "";
	ИсправленыОсновныеПараметры = Ложь;
	
	Если ФорматноЛогическийКонтрольВызовСервера.ВыполненаПроверкаОбязательностиИПравильностиЗаполненияТэгов(ОбщиеПараметры, ОборудованиеУстройстваПечати, ОписаниеОшибки) Тогда
		ФорматноЛогическийКонтрольКлиентСервер.ПривестиДанныеКТребуемомуФормату(ОбщиеПараметры, Отказ, ОписаниеОшибки, ИсправленыОсновныеПараметры);
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
	Если Отказ Тогда
		Сообщить(ОписаниеОшибки);
		Возврат;
	КонецЕсли;
	
	Если МенеджерОборудованияКлиент.ВыполнитьФискализациюЧекаНаФискальномУстройстве(УникальныйИдентификатор, ОборудованиеУстройстваПечати, ОбщиеПараметры, ВыходныеПараметры) Тогда
		НомерЧека = ВыходныеПараметры.НомерЧека;
	Иначе
		Сообщить(ВыходныеПараметры.ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ККТ_ЧекКоррекции(Команда)
	
	ОчиститьСообщения();
	
	ВыходныеПараметры = Неопределено;
	
	Если НеприменениеККТ Тогда
		// Документ с правильными реквизитами
		ОбщиеПараметры = МенеджерОборудованияКлиентСервер.ПараметрыОперацииЧекаКоррекции();
		ЗаполнитьШаблонЧека(ОбщиеПараметры); 
		// Заполняем параметры для коррекции
		ОбщиеПараметры.ДополнительныйРеквизит = ДокументОснованиеФП;   // Фискальный признак документа основания.
		ОбщиеПараметры.ДанныеКоррекции.ТипКоррекции      = ТипКоррекции;
		ОбщиеПараметры.ДанныеКоррекции.ОписаниеКоррекции = ОписаниеКоррекции;
		ОбщиеПараметры.ДанныеКоррекции.ДатаКоррекции     = ДатаКоррекции;
		ОбщиеПараметры.ДанныеКоррекции.НомерПредписания  = НомерПредписания;
		ОбщиеПараметры.НеприменениеККТ = НеприменениеККТ; 
		Если НЕ МенеджерОборудованияКлиент.ВыполнитьФискализациюЧекаКоррекцииНаФискальномУстройстве(УникальныйИдентификатор, ОборудованиеУстройстваПечати, ОбщиеПараметры, ВыходныеПараметры) Тогда
			Сообщить(ВыходныеПараметры.ТекстОшибки);
		КонецЕсли;
	Иначе
		// Общие параметры для формирования чека коррекции
		
		// Документ с правильными реквизитами
		ОбщиеПараметры = МенеджерОборудованияКлиентСервер.ПараметрыОперацииЧекаКоррекции();
		ЗаполнитьШаблонЧека(ОбщиеПараметры); 
		ОбщиеПараметры.ДополнительныйРеквизит = ДокументОснованиеФП;   // Фискальный признак документа основания.
		ОбщиеПараметры.ДанныеКоррекции.ТипКоррекции      = ТипКоррекции;
		ОбщиеПараметры.ДанныеКоррекции.ОписаниеКоррекции = ОписаниеКоррекции;
		ОбщиеПараметры.ДанныеКоррекции.ДатаКоррекции     = ДатаКоррекции;
		ОбщиеПараметры.ДанныеКоррекции.НомерПредписания  = НомерПредписания;
		ОбщиеПараметры.НеприменениеККТ = НеприменениеККТ; 
	
		// Документ, содержащий ошибки, для которого будет сформирован чек сторно.
		Если МенеджерОборудованияКлиент.ВыполнитьФискализациюЧекаКоррекцииНаФискальномУстройстве(УникальныйИдентификатор, ОборудованиеУстройстваПечати, ОбщиеПараметры, ВыходныеПараметры) Тогда
			
			// Документ, содержащий правильные реквизиты.
			ОбщиеПараметры = МенеджерОборудованияКлиентСервер.ПараметрыОперацииЧекаКоррекции();
			ЗаполнитьШаблонЧека(ОбщиеПараметры); 
			ОбщиеПараметры.ДополнительныйРеквизит = ДокументОснованиеФП;   // Фискальный признак документа основания.
			ОбщиеПараметры.ДанныеКоррекции.ТипКоррекции      = ТипКоррекции;
			ОбщиеПараметры.ДанныеКоррекции.ОписаниеКоррекции = ОписаниеКоррекции;
			ОбщиеПараметры.ДанныеКоррекции.ДатаКоррекции     = ДатаКоррекции;
			ОбщиеПараметры.ДанныеКоррекции.НомерПредписания  = НомерПредписания;
			ОбщиеПараметры.НеприменениеККТ = НеприменениеККТ; 
			
			Если НЕ МенеджерОборудованияКлиент.ВыполнитьФискализациюЧекаНаФискальномУстройстве(УникальныйИдентификатор, ОборудованиеУстройстваПечати, ОбщиеПараметры, ВыходныеПараметры) Тогда
				Сообщить(ВыходныеПараметры.ТекстОшибки);
			КонецЕсли;
		Иначе
			Сообщить(ВыходныеПараметры.ТекстОшибки);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ККТ_ВнестиСумму(Команда)
	
	ОчиститьСообщения();
	
	ВходныеПараметры = МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперации();
	ВходныеПараметры.Вставить("Внесение", Истина);
	ВходныеПараметры.Вставить("Сумма"   , Сумма);
	
	ПредзаполнитьПараметрыЧека(ВходныеПараметры);
	
	ВыходныеПараметры = Неопределено;
	Если НЕ МенеджерОборудованияКлиент.ВыполнитьВнесениеВыемку(УникальныйИдентификатор, ОборудованиеУстройстваПечати, ВходныеПараметры, ВыходныеПараметры) Тогда
		Сообщить(ВыходныеПараметры.ТекстОшибки);
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ККТ_ИзъятьСумму(Команда)
	
	ОчиститьСообщения();
	
	ВходныеПараметры = МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперации();
	ВходныеПараметры.Вставить("Внесение", Ложь);
	ВходныеПараметры.Вставить("Сумма"   , Сумма);
	
	ПредзаполнитьПараметрыЧека(ВходныеПараметры);
	
	ВыходныеПараметры = Неопределено;
	Если НЕ МенеджерОборудованияКлиент.ВыполнитьВнесениеВыемку(УникальныйИдентификатор, ОборудованиеУстройстваПечати, ВходныеПараметры, ВыходныеПараметры) Тогда
		Сообщить(ВыходныеПараметры.ТекстОшибки);
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ККТ_ПечатьТекста(Команда)
	
	ОчиститьСообщения();
	ВыходныеПараметры = Неопределено;
	ВходныеПараметры = Новый Структура("СтрокиТекста", СтрокиТекста);
	Если НЕ МенеджерОборудованияКлиент.ВыполнитьПечатьТекста(УникальныйИдентификатор, ОборудованиеУстройстваПечати, ВходныеПараметры, ВыходныеПараметры) Тогда
		Сообщить(ВыходныеПараметры.ТекстОшибки);
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ККТ_ОтчетОТекущемСостоянииРасчетов(Команда)
	
	ОчиститьСообщения();
	
	ТекстОтчета = "СУММА" + Символы.Таб + Строка(10.45);
	
	ВходныеПараметры = МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперации();
	ВходныеПараметры.Вставить("ТекстОтчета", ТекстОтчета);
	
	ПредзаполнитьПараметрыЧека(ВходныеПараметры);

	ВыходныеПараметры = Неопределено;
	Если НЕ МенеджерОборудованияКлиент.ВыполнитьФормированиеОтчетаОТекущемСостоянииРасчетов(УникальныйИдентификатор, ОборудованиеУстройстваПечати, ВходныеПараметры, ВыходныеПараметры) Тогда
		Сообщить(ВыходныеПараметры.ТекстОшибки);
	КонецЕсли

КонецПроцедуры

&НаКлиенте
Процедура ККТ_ОтчетБезГашения(Команда)
	
	ОчиститьСообщения();
	
	ТекстОтчета = "СУММА" + Символы.Таб + Строка(10.45);
	
	ВходныеПараметры = МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперации();
	ВходныеПараметры.Вставить("ТекстОтчета", ТекстОтчета);
	
	ПредзаполнитьПараметрыЧека(ВходныеПараметры);
	
	ВыходныеПараметры = Неопределено;
	Если НЕ МенеджерОборудованияКлиент.ВыполнитьФормированиеОтчетаБезГашения(УникальныйИдентификатор, ОборудованиеУстройстваПечати, ВходныеПараметры, ВыходныеПараметры) Тогда
		Сообщить(ВыходныеПараметры.ТекстОшибки);
	КонецЕсли

КонецПроцедуры

&НаКлиенте
Процедура ККТ_СостояниеККТ(Команда)
	
	ОчиститьСообщения();
	ВходныеПараметры = МенеджерОборудованияКлиентСервер.ПараметрыВыполненияОперации();
	ВыходныеПараметры = Неопределено;
	Если МенеджерОборудованияКлиент.ВыполнитьПолучениеТекущегоСостоянияФискальногоУстройства(УникальныйИдентификатор, ОборудованиеУстройстваПечати, ВходныеПараметры, ВыходныеПараметры) Тогда
		ВывестиСтруктуруНаЭкран(ВыходныеПараметры);
	Иначе
		Сообщить(ВыходныеПараметры.ТекстОшибки);
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ККТ_ПараметрыККТ(Команда)
	
	ОчиститьСообщения();
	ВходныеПараметры  = Неопределено;
	ВыходныеПараметры = Неопределено;
	Если МенеджерОборудованияКлиент.ВыполнитьПолучениеПараметровФискальногоУстройства(УникальныйИдентификатор, ОборудованиеУстройстваПечати, ВходныеПараметры, ВыходныеПараметры) Тогда
		ВывестиСтруктуруНаЭкран(ВыходныеПараметры);
	Иначе
		Сообщить(ВыходныеПараметры.ТекстОшибки);
	КонецЕсли

КонецПроцедуры

&НаКлиенте
Процедура ККТ_ТекстЧека(Команда)
	
	ОчиститьСообщения();
	
	ШиринаСтроки = 32;
	ВыходныеПараметры = Неопределено;
	Если МенеджерОборудованияКлиент.ПолучитьШиринуСтрокиПечатающегоУстройства(УникальныйИдентификатор, ОборудованиеУстройстваПечати, Неопределено, ВыходныеПараметры) Тогда
		ШиринаСтроки = ВыходныеПараметры.ШиринаСтроки;
	КонецЕсли;   
	
	ВыходныеПараметры = Неопределено;
	
	ОбщиеПараметры = МенеджерОборудованияКлиентСервер.ПараметрыОперацииФискализацииЧека();
	ЗаполнитьШаблонЧека(ОбщиеПараметры, Истина);
	ПредзаполнитьПараметрыЧека(ОбщиеПараметры);
	
	ФорматноЛогическийКонтрольКлиентСервер.ПровестиФорматноЛогическийКонтроль(ОбщиеПараметры, ОборудованиеУстройстваПечати);
	
	Текст = МенеджерОборудованияКлиентСервер.СформироватьТекстНефискальногоЧека(ОбщиеПараметры, ШиринаСтроки);
	
	ВыходныеПараметры = Неопределено;
	ВходныеПараметры = Новый Структура("СтрокиТекста", Текст);
	Если НЕ МенеджерОборудованияКлиент.ВыполнитьПечатьТекста(УникальныйИдентификатор, ОборудованиеУстройстваПечати, ВходныеПараметры, ВыходныеПараметры) Тогда
		Сообщить(ВыходныеПараметры.ТекстОшибки);
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ККТ_ТекстИнфоЧека(Команда)
	
	ОчиститьСообщения();
	
	ШиринаСтроки = 32;
	ВыходныеПараметры = Неопределено;
	Если МенеджерОборудованияКлиент.ПолучитьШиринуСтрокиПечатающегоУстройства(УникальныйИдентификатор, ОборудованиеУстройстваПечати, Неопределено, ВыходныеПараметры) Тогда
		ШиринаСтроки = ВыходныеПараметры.ШиринаСтроки;
	КонецЕсли;   
	
	ВыходныеПараметры = Неопределено;
	
	ОбщиеПараметры = МенеджерОборудованияКлиентСервер.ПараметрыОперацииФискализацииЧека();
	ЗаполнитьШаблонЧека(ОбщиеПараметры, Истина);
	
	ПредзаполнитьПараметрыЧека(ОбщиеПараметры);
	ФорматноЛогическийКонтрольКлиентСервер.ПровестиФорматноЛогическийКонтроль(ОбщиеПараметры, ОборудованиеУстройстваПечати);
	
	ПараметрыШтрихкода = МенеджерОборудованияКлиентСервер.ПараметрыШтрихкодВСтрокеЧека("EAN13", "2900001462105");
	Текст = МенеджерОборудованияКлиентСервер.СформироватьИнформационныйЧек(ОбщиеПараметры, ШиринаСтроки,, ПараметрыШтрихкода);
	
	ВыходныеПараметры = Неопределено;
	ВходныеПараметры = Новый Структура("СтрокиТекста", Текст);
	Если НЕ МенеджерОборудованияКлиент.ВыполнитьПечатьТекста(УникальныйИдентификатор, ОборудованиеУстройстваПечати, ВходныеПараметры, ВыходныеПараметры) Тогда
		Сообщить(ВыходныеПараметры.ТекстОшибки);
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ККТ_ПодготовитьКМ(Команда)
	
	ККТ_КодКонтрольнойМаркиСтрока = СтрЗаменить(ККТ_КодКонтрольнойМаркиСтрока, "\u001d", МенеджерОборудованияМаркировкаКлиентСервер.РазделительGS1());
	ККТ_КодКонтрольнойМарки = МенеджерОборудованияКлиентСервер.ШтрихкодВBase64(ККТ_КодКонтрольнойМаркиСтрока);
	ККТ_КодКонтрольнойМаркиСтрока = СтрЗаменить(ККТ_КодКонтрольнойМаркиСтрока, МенеджерОборудованияМаркировкаКлиентСервер.РазделительGS1(), "\u001d");
	
КонецПроцедуры

&НаКлиенте
Процедура ККТ_ОткрытьСессиюРегистрацииКМ(Команда)
	
	ВходныеПараметры = Неопределено;
	ВыходныеПараметры = Неопределено;
	
	Если НЕ МенеджерОборудованияКлиент.НачатьОткрытиеСессииРегистрацииКМ(УникальныйИдентификатор, ОборудованиеУстройстваПечати, ВходныеПараметры, ВыходныеПараметры) Тогда
		Сообщить(ВыходныеПараметры.ТекстОшибки);
	Иначе
		Сообщить("Выполнено");
	КонецЕсли

КонецПроцедуры

&НаКлиенте
Процедура ККТ_ЗакрытьСессиюРегистрацииКМ(Команда)
	
	ВходныеПараметры = Неопределено;
	ВыходныеПараметры = Неопределено;
	
	Если НЕ МенеджерОборудованияКлиент.НачатьЗакрытииСессииРегистрацииКМ(УникальныйИдентификатор, ОборудованиеУстройстваПечати, ВходныеПараметры, ВыходныеПараметры) Тогда
		Сообщить(ВыходныеПараметры.ТекстОшибки);
	Иначе
		Сообщить("Выполнено");
	КонецЕсли

КонецПроцедуры

&НаКлиенте
Процедура ККТ_ЗапросКМ(Команда)
	
	ШтучныйТовар = ККТ_ПланируемыйСтатусТовара = ПредопределенноеЗначение("Перечисление.ПланируемыйСтатусМаркируемогоТовара.ШтучныйТоварРеализован")
		Или ККТ_ПланируемыйСтатусТовара = ПредопределенноеЗначение("Перечисление.ПланируемыйСтатусМаркируемогоТовара.ШтучныйТоварВозвращен");
		
	ККТ_УникальныйИдентификаторЗапроса = Строка(Новый УникальныйИдентификатор); 
	
	ПараметрыОперации = МенеджерОборудованияКлиентСервер.ПараметрыЗапросКМ();
	ПараметрыОперации.ИдентификаторЗапроса = ККТ_УникальныйИдентификаторЗапроса;
	Если Не ШтучныйТовар Тогда
		ПараметрыОперации.Количество = ККТ_Количество;  
		ПараметрыОперации.ДробноеКоличество.Числитель   = ККТ_ДробноеКоличествоЧислитель;     
		ПараметрыОперации.ДробноеКоличество.Знаменатель = ККТ_ДробноеКоличествоЗнаменатель;     
		ПараметрыОперации.МераКоличестваПредметаРасчета = ККТ_МераКоличества;
	КонецЕсли;
	
	ПараметрыОперации.ПланируемыйСтатусТовара = ККТ_ПланируемыйСтатусТовара;
	ПараметрыОперации.КонтрольнаяМарка = ККТ_КодКонтрольнойМарки;
	ПараметрыОперации.ОжидатьПолучениеОтветаОИСМ = ККТ_ОжидатьПолучениеОтветаОИСМ;
	ВыходныеПараметры = Неопределено;
	
	Если МенеджерОборудованияКлиент.НачатьЗапросКМ(УникальныйИдентификатор, ОборудованиеУстройстваПечати, ПараметрыОперации, ВыходныеПараметры) Тогда
		ККТ_КомандаВыполнитьЗавершениеКМ(ВыходныеПараметры)
	Иначе
		Сообщить(ВыходныеПараметры.ТекстОшибки);
	КонецЕсли

КонецПроцедуры

&НаКлиенте
Процедура ККТ_ПодтверждениеКМ(Команда)
	
	ПараметрыОперации = МенеджерОборудованияКлиентСервер.ПараметрыПодтверждениеКМ();
	ПараметрыОперации.ИдентификаторЗапроса = ККТ_УникальныйИдентификаторЗапроса;
	ПараметрыОперации.Выбытие = ККТ_БудетРеализована;
	
	ВыходныеПараметры = Неопределено;
	
	Если МенеджерОборудованияКлиент.НачатьПодтверждениеКМ(УникальныйИдентификатор, ОборудованиеУстройстваПечати, ПараметрыОперации, ВыходныеПараметры) Тогда
		ККТ_КомандаВыполнитьЗавершениеКМ(ВыходныеПараметры)
	Иначе
		Сообщить(ВыходныеПараметры.ТекстОшибки);
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ККТ_ПолучитьРезультатыЗапросаКМ(Команда)
	
	ВыходныеПараметры = Неопределено;
	ПараметрыОперации = Неопределено;
	
	Если МенеджерОборудованияКлиент.НачатьПолученияРезультатовЗапросаКМ(УникальныйИдентификатор, ОборудованиеУстройстваПечати, ПараметрыОперации, ВыходныеПараметры) Тогда
		ККТ_КомандаВыполнитьЗавершениеКМ(ВыходныеПараметры)
	Иначе
		Сообщить(ВыходныеПараметры.ТекстОшибки);
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ККТ_КомандаВыполнитьЗавершениеКМ(Результат) Экспорт
	
	ОчиститьСообщения();
	
	ККТ_КодМаркировкиПроверенФискальнымНакопителем = Ложь;
	ККТ_СтатусРезультатаЗапросаКМ = Неопределено;
	ККТ_РезультатПроверкиОИСМПредставление = Неопределено;
	ККТ_РезультатПроверкиФискальнымНакопителемПоложительный = Ложь;
	ККТ_РезультатПроверки = Ложь;
	ККТ_РезультатПроверкиСведенийОТоваре = Неопределено;
	ККТ_РезультатПроверкиСведенийОТовареПФ = Неопределено;
	
	ТекстСообщения = НСтр("ru='Операция завершена.'");
	
	Если Результат.Свойство("СтатусРезультата") Тогда
		ККТ_СтатусРезультатаЗапросаКМ = Результат.СтатусРезультата;
	КонецЕсли;
	Если Результат.Свойство("КодМаркировкиПроверен") Тогда
		ККТ_КодМаркировкиПроверенФискальнымНакопителем = Результат.КодМаркировкиПроверен;
	КонецЕсли;
	Если Результат.Свойство("РезультатПроверки") Тогда
		ККТ_РезультатПроверкиФискальнымНакопителемПоложительный = Результат.РезультатПроверки;
	КонецЕсли;
	Если Результат.Свойство("РезультатПроверкиОИСМ") Тогда
		ККТ_РезультатПроверки = Результат.РезультатПроверкиОИСМ;
	КонецЕсли;
	Если Результат.Свойство("РезультатПроверкиОИСМПредставление") Тогда
		ККТ_РезультатПроверкиОИСМПредставление = Результат.РезультатПроверкиОИСМПредставление;
	КонецЕсли;
	Если Результат.Свойство("СтатусОбработкиЗапроса") Тогда
		ККТ_СтатусОбработкиЗапроса = Результат.СтатусОбработкиЗапроса;
	КонецЕсли;
	Если Результат.Свойство("СтатусТовара") Тогда
		ККТ_ОтветОИСМОСтатусеТовара = Результат.СтатусТовара;
	КонецЕсли;
	Если Результат.Свойство("ИдентификаторСессии") Тогда
		ККТ_ИдентификаторСессии = Результат.ИдентификаторСессии;
	КонецЕсли;
	Если Результат.Свойство("РезультатПроверкиСведенийОТоваре") Тогда
		ККТ_РезультатПроверкиСведенийОТоваре = Результат.РезультатПроверкиСведенийОТоваре;
	КонецЕсли;
	Если Результат.Свойство("РезультатПроверкиСведенийОТовареПФ") Тогда
		ККТ_РезультатПроверкиСведенийОТоваре = Результат.РезультатПроверкиСведенийОТовареПФ + " " + ККТ_РезультатПроверкиСведенийОТоваре;
	КонецЕсли;
	
	Сообщить("Выполнено.");
	
КонецПроцедуры

&НаКлиенте
Процедура ККТ_ПланируемыйСтатусТовараПриИзменении(Элемент)
	
	ШтучныйТовар = ККТ_ПланируемыйСтатусТовара = ПредопределенноеЗначение("Перечисление.ПланируемыйСтатусМаркируемогоТовара.ШтучныйТоварРеализован")
			Или ККТ_ПланируемыйСтатусТовара = ПредопределенноеЗначение("Перечисление.ПланируемыйСтатусМаркируемогоТовара.ШтучныйТоварВозвращен");
	
	Элементы.ККТ_МераКоличества.Доступность = НЕ ШтучныйТовар;
	Элементы.ККТ_Количество.Доступность = НЕ ШтучныйТовар;
	Элементы.ККТ_ДробноеКоличествоЧислитель.Доступность = НЕ ШтучныйТовар;
	Элементы.ККТ_ДробноеКоличествоЗнаменатель.Доступность = НЕ  ШтучныйТовар;
	
КонецПроцедуры

&НаКлиенте
Процедура УВ_ПодключениеОборудование(Команда)
	
	ОписаниеОшибки = "";
	Если НЕ МенеджерОборудованияКлиент.ПодключитьОборудованиеПоИдентификатору(УникальныйИдентификатор, ОборудованиеУстройствоВвода, ОписаниеОшибки) Тогда
		Сообщить(ОписаниеОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УВ_ОтключитьОборудование(Команда)
	
	ОписаниеОшибки = "";
	Если НЕ МенеджерОборудованияКлиент.ОтключитьОборудованиеПоИдентификатору(УникальныйИдентификатор, ОборудованиеУстройствоВвода, ОписаниеОшибки) Тогда
		Сообщить(ОписаниеОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВО_ПолучитьВесСЭлектронныхВесов(Команда)
	
	ВыходныеПараметры = Неопределено;
	Если НЕ МенеджерОборудованияКлиент.ВыполнитьПолучениеВесаСЭлектронныхВесов(УникальныйИдентификатор, ОборудованиеПериферия, ВыходныеПараметры) Тогда
		Сообщить(ВыходныеПараметры.ТекстОшибки);
	Иначе
		Сообщить(ВыходныеПараметры.Вес);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДП_ВыводИнформацииНаДисплейПокупателя(Команда)
	
	ВходныеПараметры = МенеджерОборудованияКлиентСервер.ПараметрыОперацииДисплейПокупателя(ДП_Текст);
	ВыходныеПараметры = Неопределено;
	Если НЕ МенеджерОборудованияКлиент.ВыполнитьВыводИнформацииНаДисплейПокупателя(УникальныйИдентификатор, ОборудованиеПериферия, ВходныеПараметры, ВыходныеПараметры) Тогда
		Сообщить(ВыходныеПараметры.ТекстОшибки);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДП_ВыполнитьОчисткуДисплеяПокупателя(Команда)
	
	ВыходныеПараметры = Неопределено;
	Если НЕ МенеджерОборудованияКлиент.ВыполнитьОчисткуДисплеяПокупателя(УникальныйИдентификатор, ОборудованиеПериферия, ВыходныеПараметры) Тогда
		Сообщить(ВыходныеПараметры.ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры            

&НаКлиенте
Процедура ДП_ВыполнитьВыводQRКода(Команда)
	
	ВходныеПараметры = МенеджерОборудованияКлиентСервер.ПараметрыОперацииДисплейПокупателя();
	ВходныеПараметры.ЗначениеQRКода = ДП_ЗначениеQRКода;
	
	ВыходныеПараметры = Неопределено;
	Если НЕ МенеджерОборудованияКлиент.ВыполнитьВыводQRКодаНаДисплеяПокупателя(УникальныйИдентификатор, ОборудованиеПериферия, ВходныеПараметры, ВыходныеПараметры) Тогда
		Сообщить(ВыходныеПараметры.ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиСтруктуруНаЭкран(СтруктурыДляВывода)  
	
	Для Каждого СвойствоСтруктуры Из СтруктурыДляВывода Цикл
		Если ТипЗнч(СвойствоСтруктуры.Значение) = Тип("Структура") Тогда 
			ВывестиСтруктуруНаЭкран(СвойствоСтруктуры.Значение) 
		Иначе
			Сообщить(СвойствоСтруктуры.Ключ + "=" + СвойствоСтруктуры.Значение);
		КонецЕсли
	КонецЦикла;
	
КонецПроцедуры    

&НаКлиенте
Процедура НеприменениеККТПриИзменении(Элемент)
	
	Элементы.ДополнительныйРеквизит.Доступность = НЕ НеприменениеККТ; 
	               
КонецПроцедуры

&НаКлиенте
Процедура УВ_ВыборКодаВидаНоменклатурнойКлассификации(Команда)
	
	ОповещениеПриВыборе = Новый ОписаниеОповещения("УВ_ВыборКодаВидаНоменклатурнойКлассификации_Завершение", ЭтотОбъект);                                 
	МенеджерОборудованияКлиент.НачатьВыборКодаВидаНоменклатурнойКлассификации(ОповещениеПриВыборе);
	
КонецПроцедуры

&НаКлиенте
Процедура УВ_ВыборКодаВидаНоменклатурнойКлассификации_Завершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		 Штрихкод = Результат.КодВидаНоменклатурнойКлассификации;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура RFID_ОткрытьСессию(Команда)
	
	ОчиститьСообщения();
	ВыходныеПараметры = Неопределено;
	
	Если НЕ МенеджерОборудованияКлиент.ВыполнитьОткрытиеСессииСчитывателяRFID(УникальныйИдентификатор, ОборудованиеRFID, ВыходныеПараметры) Тогда
		Сообщить(ВыходныеПараметры.ТекстОшибки);
	КонецЕсли;   
	
КонецПроцедуры

&НаКлиенте
Процедура RFID_ЗакрытьСессию(Команда)
	
	ОчиститьСообщения();
	ВыходныеПараметры = Неопределено;
	
	Если НЕ МенеджерОборудованияКлиент.ВыполнитьЗакрытиеСессииСчитывателяRFID(УникальныйИдентификатор, ОборудованиеRFID, ВыходныеПараметры) Тогда
		Сообщить(ВыходныеПараметры.ТекстОшибки);
	КонецЕсли;   
	
КонецПроцедуры

&НаКлиенте
Процедура RFID_ЗаписатьВМеткуEPC(Команда)
	
	Если Не ЗначениеЗаполнено(ОборудованиеRFID) Тогда
		ТекстСообщения = НСтр("ru='Не выбран считыватель RFID.'");
		Сообщить(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	ВыходныеПараметры = Неопределено;

	ТекущаяСрока = Элементы.МеткиRFID.ТекущиеДанные;
	Если ТекущаяСрока <> Неопределено Тогда
		ПараметрыЗаписи = МенеджерОборудованияКлиент.ПараметрыЗаписиМеткиRFID();
		ПараметрыЗаписи.TID = ТекущаяСрока.TID;
		ПараметрыЗаписи.EPC = ТекущаяСрока.EPC;
		ПараметрыЗаписи.БанкПамяти = "EPC";
		ПараметрыЗаписи.Данные = МенеджерОборудованияКлиентСервер.СформироватьДанныеSGTIN96(ТекущаяСрока.GTIN, ТекущаяСрока.СерийныйНомер);
		Если НЕ МенеджерОборудованияКлиент.ВыполнитьЗаписьДанныхВМеткуRFID(УникальныйИдентификатор, ОборудованиеRFID, ПараметрыЗаписи, ВыходныеПараметры) Тогда
			Сообщить(ВыходныеПараметры.ТекстОшибки);
		КонецЕсли;   
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура RFID_ЗаписатьВМеткуGTINSerial(Команда)
	
	Если Не ЗначениеЗаполнено(ОборудованиеRFID) Тогда
		ТекстСообщения = НСтр("ru='Не выбран считыватель RFID.'");
		Сообщить(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	ВыходныеПараметры = Неопределено;
	
	ТекущаяСрока = Элементы.МеткиRFID.ТекущиеДанные;
	Если ТекущаяСрока <> Неопределено Тогда
		ПараметрыЗаписи = МенеджерОборудованияКлиент.ПараметрыЗаписиМеткиRFID();
		ПараметрыЗаписи.TID = ТекущаяСрока.TID;
		ПараметрыЗаписи.EPC = ТекущаяСрока.EPC;
		ПараметрыЗаписи.БанкПамяти = "EPC";
		ПараметрыЗаписи.Данные = МенеджерОборудованияКлиентСервер.СформироватьДанныеSGTIN96(ТекущаяСрока.GTIN, ТекущаяСрока.СерийныйНомер);
		Если НЕ МенеджерОборудованияКлиент.ВыполнитьЗаписьДанныхВМеткуRFID(УникальныйИдентификатор, ОборудованиеRFID, ПараметрыЗаписи, ВыходныеПараметры) Тогда
			Сообщить(ВыходныеПараметры.ТекстОшибки);
		КонецЕсли;   
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура RFID_ЗаписатьВМеткуUserMemory(Команда)
	
	Если Не ЗначениеЗаполнено(ОборудованиеRFID) Тогда
		ТекстСообщения = НСтр("ru='Не выбран считыватель RFID.'");
		Сообщить(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	ВыходныеПараметры = Неопределено;
	
	ТекущаяСрока = Элементы.МеткиRFID.ТекущиеДанные;
	Если ТекущаяСрока <> Неопределено Тогда
		ПараметрыЗаписи = МенеджерОборудованияКлиент.ПараметрыЗаписиМеткиRFID();
		ПараметрыЗаписи.TID = ТекущаяСрока.TID;
		ПараметрыЗаписи.EPC = ТекущаяСрока.EPC;
		ПараметрыЗаписи.БанкПамяти = "USER";
		ПараметрыЗаписи.Данные = МенеджерОборудованияКлиентСервер.ПреобразоватьСтрокуВHEX(ТекущаяСрока.UserMemory);
		Если НЕ МенеджерОборудованияКлиент.ВыполнитьЗаписьДанныхВМеткуRFID(УникальныйИдентификатор, ОборудованиеRFID, ПараметрыЗаписи, ВыходныеПараметры) Тогда
			Сообщить(ВыходныеПараметры.ТекстОшибки);
		КонецЕсли;   
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура RFID_ГенерироватьСерийныйНомер(Команда)
	
	ТекущаяСрока = Элементы.МеткиRFID.ТекущиеДанные;
	Если ТекущаяСрока <> Неопределено Тогда
		TID = ТекущаяСрока.TID;
		EPC = ТекущаяСрока.EPC;
		РезультатВыполнения = МенеджерОборудованияКлиентСервер.ПолучитьСерийныйНомерПоTID(TID, EPC);
		Если РезультатВыполнения.Результат Тогда
			ТекущаяСрока.СерийныйНомер = Формат(РезультатВыполнения.СерийныйНомер, "ЧВН=; ЧГ=");
		Иначе
			Сообщить(РезультатВыполнения.ОписаниеОшибки);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РазобратьШК(Команда)
	
	ВремДанныеСканера = СтрЗаменить(Штрихкод, "\u001d", МенеджерОборудованияМаркировкаКлиентСервер.РазделительGS1());
	ОбработатьШтрихкод(ВремДанныеСканера);
	
КонецПроцедуры

