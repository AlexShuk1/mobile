
#Область ОбработчикиСобытийФормы  


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(ДрайверыОборудования.Ссылка) КАК КоличествоДрайверов
		|ИЗ
		|	Справочник.ДрайверыОборудования КАК ДрайверыОборудования";
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Если ВыборкаДетальныеЗаписи.Следующий() И ВыборкаДетальныеЗаписи.КоличествоДрайверов = 0 Тогда
		МенеджерОборудованияВызовСервераПереопределяемый.ОбновитьПоставляемыеДрайвера();
	КонецЕсли;
	
	#Если МобильноеПриложениеСервер Тогда
		Элементы.ОткрытьРабочиеМеста.Видимость = Ложь;
	#КонецЕсли    
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы["СтраницаОсновное"];           
	
		
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "УспешноеПодключениеКИнформационнойБазе" Тогда
		Элементы.СтраницаОсновное.Видимость = Ложь;     
		Элементы.СтраницаДействия.Видимость = Истина;
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы["СтраницаДействия"];
		ОбновитьДействияНаКлиенте();	
	ИначеЕсли ИмяСобытия = "ОтключениеОтИнформационнойБазы" Тогда
		SS_СлужебныйСервер.ОтключитьсяОтТекущейБазыБазы();	   
		Элементы.СтраницаОсновное.Видимость = Истина;     
		Элементы.СтраницаДействия.Видимость = Ложь;
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы["СтраницаОсновное"]; 
		ТаблицаДействий.Очистить();
	КонецЕсли;

КонецПроцедуры


#КонецОбласти     

#Область ОбработчикиСобытийЭлементовФормы    


&НаКлиенте
Процедура ТаблицаДействийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекДанные = Элементы.ТаблицаДействий.ТекущиеДанные;     
	
	
	Если ТекДанные <> Неопределено Тогда
		Попытка		
			SS_СлужебныйКлиентСервер.ОбработатьСобытиеДействиеВыбор(ЭтаФорма, ТекДанные.Действие); 	
		Исключение   
			Сообщение = Новый СообщениеПользователю;   
			ТекстОшибки = ОписаниеОшибки();
			Сообщение.Текст = "Ошибка приложения. Обратитесь в поддержку";
			Сообщение.Сообщить();			
		КонецПопытки;
	Иначе
		Сообщение = Новый СообщениеПользователю;   
		Сообщение.Текст = "Не выбрано действие";
		Сообщение.Сообщить();	
	КонецЕсли;	
	
	
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиКомандФормы 

&НаКлиенте
Процедура Выход(Команда)
	
	SS_СлужебныйСервер.ОтключитьсяОтТекущейБазыБазы();	   
	Элементы.СтраницаОсновное.Видимость = Истина;     
	Элементы.СтраницаДействия.Видимость = Ложь;
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы["СтраницаОсновное"]; 
	ТаблицаДействий.Очистить();

КонецПроцедуры


&НаКлиенте
Процедура ОбновитьДействия(Команда)
	ОбновитьДействияНаКлиенте();
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции 


&НаКлиенте
Процедура ОбновитьДействияНаКлиенте()
	
	Попытка
		
		ТекущееПодключение = SS_СлужебныйСервер.ВернутьТекущееПодключниеКбазе();
		
		Если ТекущееПодключение.База <> Неопределено Тогда
			Соединение = SS_СлужебныйКлиентСервер.ПолучитьСоединениеСБазой(ТекущееПодключение);	
			ЗапросHTTP = SS_СлужебныйКлиентСервер.ПолучитьHTTPЗапрос();
			ЗапросHTTP.АдресРесурса = ТекущееПодключение.ПараметрыПодключения.ИмяБазы+
			"/hs/tsd/actionslist/"+
			ТекущееПодключение.ШтрихкодСотрудника;  
			Ответ = Соединение.Получить(ЗапросHTTP);   
			
			Если Ответ.КодСостояния = 200 Тогда     
				
				ПолученныеДанные = SS_СлужебныйКлиентСервер.ПрочитатьТелоJSON(Ответ);
				Если ПолученныеДанные.status = "success" Тогда       
					ТаблицаДействий.Очистить();	
					Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы["СтраницаДействия"];
					
					Для Каждого СтрокаДействий из ПолученныеДанные.data.actions Цикл
						СтрокаТаблицыДействий = ТаблицаДействий.Добавить();
						СтрокаТаблицыДействий.Действие = СтрокаДействий; 
					КонецЦикла;
					
				Иначе  
					ТаблицаДействий.Очистить();
					Сообщение = Новый СообщениеПользователю;
					Сообщение.Текст = "Ошибка получения действий: "+ПолученныеДанные.message;
					Сообщение.Сообщить();
				КонецЕсли;  
				
			Иначе   
				ТаблицаДействий.Очистить();
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Ошибка подключения. Код ответа "+Ответ.КодСостояния;
				Сообщение.Сообщить();
			КонецЕсли;  				
			
		КонецЕсли;    
		
	Исключение  
		ТаблицаДействий.Очистить();
		Сообщение = Новый СообщениеПользователю;   
		ТекстОшибки = ОписаниеОшибки();
		Сообщение.Текст = "Ошибка приложения. Обратитесь в поддержку";
		Сообщение.Сообщить();		
	КонецПопытки
	
	
КонецПроцедуры


#КонецОбласти

#Область БСП



&НаКлиенте
Процедура ОткрытьДемоТестированиеОборудования(Команда)
	
	ОткрытьФорму("Обработка.ДемоТестированиеОборудования.Форма.Форма",, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДрайверыОборудования(Команда)
	
	ОткрытьФорму("Справочник.ДрайверыОборудования.ФормаСписка",, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПодключаемоеОборудование(Команда)
	
	ОткрытьФорму("Справочник.ПодключаемоеОборудование.ФормаСписка",, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьРабочиеМеста(Команда)
	
	ОткрытьФорму("Справочник.РабочиеМеста.ФормаСписка",, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПоставляемыеДрайвера(Команда)
	
	МенеджерОборудованияВызовСервераПереопределяемый.ОбновитьПоставляемыеДрайвера();
	Сообщить(НСтр("ru='Операция выполнена.'"));
	
КонецПроцедуры      


#КонецОбласти


